subprojects  {
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'signing'
	
	group = 'org.maydesk'
	version = '0.1.0-SNAPSHOT'
	ext.packaging = 'jar'
	sourceCompatibility = 1.7
	
	def srcJava = 'src/main/java'
	def srcDelomboked = 'build/src-delomboked'
	def sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
	
	configurations { 
		replacer
		ajc
		aspects
		ajInpath
	}
	
	repositories {
	    mavenCentral()
	    mavenLocal()
		mavenRepo url: "https://repository.jboss.org/nexus/content/groups/public/"
	}
	
	dependencies {
	    ajc "org.aspectj:aspectjtools:1.+"
	    compile "org.projectlombok:lombok:0.11.+"
		compile files('lib/lombok_soplet.jar')
	}

	task delombok {
		// delombok task may depend on other projects already being compiled
		dependsOn configurations.compile.getTaskDependencyFromProjectDependency(true, "compileJava")
		
		// Set up incremental build, must be made in the configuration phase (not doLast)
		inputs.files file(srcJava)
		outputs.dir file(srcDelomboked)
		
		doLast {
			FileCollection collection = files(configurations.compile)
			FileCollection sumTree = collection + fileTree(dir: 'bin')
		
		    ant.taskdef(name: 'delombok', classname: 'lombok.delombok.ant.DelombokTask', classpath: configurations.compile.asPath)
			ant.delombok(from:srcJava, to:srcDelomboked, classpath: sumTree.asPath)
	    }
	}
	
	jar {
		manifest.attributes provider: 'gradle'
	}
	
	task compileJava(dependsOn: JavaPlugin.PROCESS_RESOURCES_TASK_NAME, overwrite: true)  {
		dependsOn configurations.compile.getTaskDependencyFromProjectDependency(true, "jar")
	
		// Set up incremental build and dependency to delombok task
		inputs.files delombok.outputs.files 
   		inputs.properties(["sourceCompatibility":sourceCompatibility, "targetCompatibility":targetCompatibility])
		outputs.dir sourceSets.main.output.classesDir
		
		doLast {
			ant.taskdef(resource:"org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajc.asPath)
			ant.iajc(source:sourceCompatibility, 
					target:targetCompatibility, 
					destDir:sourceSets.main.output.classesDir.absolutePath, 
					maxmem:"512m", 
					fork:"true", 
					aspectPath:configurations.aspects.asPath, 
					inpath:configurations.ajInpath.asPath, 
					forkclasspath:configurations.ajc.asPath,
					srcdir:file(srcDelomboked),
					classpath:configurations.compile.asPath)
		}
	}
	
	artifacts {
 	   archives jar
 	}
 	
 	uploadArchives {
	    repositories {
            mavenDeployer {
				repository(url: sonatypeRepositoryUrl) {
                	authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
    
                pom.project {
                   name projectName
                   packaging 'jar'
                   description projectDescription
                   url 'https://github.com/maydesk'
    
                   scm {
                       url 'https://github.com/maydesk/maydesk.git'
                       connection 'https://github.com/maydesk/maydesk.git'
                       developerConnection 'https://github.com/maydesk/maydesk.git'
                   }
    
                   licenses {
                       license {
                           name 'Mozilla Public License, v. 2.0'
                           url 'http://mozilla.org/MPL/2.0/'
                           distribution 'repo'
                       }
                   }
    
                   developers {
                       developer {
                           id 'chrismay2'
                           name 'Christof May'
                       }
                       developer {
                           id 'kriegerd'
                           name 'Alejandro Salas'
                       }
                   }
               }
            }
        }
	}
}