subprojects  {
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'signing'
	
	group = 'org.maydesk'
	version = '0.1.0-SNAPSHOT'
	ext.packaging = 'jar'
	sourceCompatibility = 1.6
	
	def srcJava = 'src/main/java'
	def srcDelomboked = 'build/src-delomboked'
 	def cloudbeesAccountName = 'maydesk'
 	def cloudbeesUsername = 'maydesk-deploy'
	def cloudbeesPassword = 'secret111'
	
	defaultTasks 'uploadArchives'
	
	configurations { 
		replacer
		ajc
		aspects
		ajInpath
		deployerJars
	}
	
	repositories {
	    mavenCentral()
	    mavenLocal()
		maven {
			url "http://repository-maydesk.forge.cloudbees.com/snapshot"
		}	
		mavenRepo url: "https://repository.jboss.org/nexus/content/groups/public/"
	}
	
	dependencies {
	    ajc "org.aspectj:aspectjtools:1.+"
	    compile "org.projectlombok:lombok:0.11.+"
		compile files(project(':modBase').file('lib/lombok_soplet.jar')) // Not the best solution. Better solved once it's on a repo
		deployerJars 'org.apache.maven.wagon:wagon-webdav:1.0-beta-2'
        deployerJars 'org.apache.maven:maven-ant-tasks:2.1.0' 
	}

	task delombok {
		// delombok task may depend on other projects already being compiled
		dependsOn configurations.compile.getTaskDependencyFromProjectDependency(true, "compileJava")
		
		// Set up incremental build, must be made in the configuration phase (not doLast)
		inputs.files file(srcJava)
		outputs.dir file(srcDelomboked)
		
		doLast {
			FileCollection collection = files(configurations.compile)
			FileCollection sumTree = collection + fileTree(dir: 'bin')
		
		    ant.taskdef(name: 'delombok', classname: 'lombok.delombok.ant.DelombokTask', classpath: configurations.compile.asPath)
			ant.delombok(from:srcJava, to:srcDelomboked, classpath: sumTree.asPath)
	    }
	}
	
	jar {
		manifest.attributes provider: 'gradle'
	}
	
	task compileJava(dependsOn: JavaPlugin.PROCESS_RESOURCES_TASK_NAME, overwrite: true)  {
		dependsOn configurations.compile.getTaskDependencyFromProjectDependency(true, "jar")
	
		// Set up incremental build and dependency to delombok task
		inputs.files delombok.outputs.files 
   		inputs.properties(["sourceCompatibility":sourceCompatibility, "targetCompatibility":targetCompatibility])
		outputs.dir sourceSets.main.output.classesDir
		
		doLast {
			ant.taskdef(resource:"org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajc.asPath)
			ant.iajc(source:sourceCompatibility, 
					target:targetCompatibility, 
					destDir:sourceSets.main.output.classesDir.absolutePath, 
					maxmem:"512m", 
					fork:"true", 
					aspectPath:configurations.aspects.asPath, 
					inpath:configurations.ajInpath.asPath, 
					forkclasspath:configurations.ajc.asPath,
					srcdir:file(srcDelomboked),
					classpath:configurations.compile.asPath)
		}
	}
	
	artifacts {
 	   archives jar
 	}
 	
 	uploadArchives {
		repositories {
			mavenDeployer {
				configuration = configurations.deployerJars
    	    	snapshotRepository(url:"dav:https://repository-${cloudbeesAccountName}.forge.cloudbees.com/snapshot/") {
    	    		authentication(userName: cloudbeesUsername, password: cloudbeesPassword)
    	    	}
        		repository(url: "dav:https://repository-${cloudbeesAccountName}.forge.cloudbees.com/release/") {
    	    		authentication(userName: cloudbeesUsername, password: cloudbeesPassword)
    	    	}
      		}
   		}
	}
}
